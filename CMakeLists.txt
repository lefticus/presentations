cmake_minimum_required(VERSION 2.8)


if(MSVC)
  find_program(WKHTMLTOPDF_EXECUTABLE 
    wkhtmltopdf
    PATHS "C:/Program Files/wkhtmltopdf/bin" "C:/Program Files (x86)/wkhtmltopdf/bin")
else()
  find_program(WKHTMLTOPDF_EXECUTABLE 
    wkhtmltopdf
    )
endif()

# wkhtmltopdf
execute_process(COMMAND ${WKHTMLTOPDF_EXECUTABLE} -V 
  OUTPUT_VARIABLE wkhtmltopdfversionout)

string(REGEX MATCH "([0-9]+\\.?)+" WKHTMLTOPDF_VER "${wkhtmltopdfversionout}")
string(FIND "${wkhtmltopdfversionout}" "with patched qt" WKHTMLTOPDF_PATCHEDQT )


if (WKHTMLTOPDF_VER VERSION_LESS "0.12.2.1")
  message(FATAL_ERROR "wkhtmltopdf version 0.12.2.1 or greater required")
endif()

if (WKHTMLTOPDF_PATCHEDQT LESS "0")
  message(FATAL_ERROR "wkhtmltopdf with special patchedqt build required")
endif()

# pandoc

find_program(PANDOC_EXECUTABLE pandoc)


execute_process(COMMAND ${PANDOC_EXECUTABLE} -v 
  OUTPUT_VARIABLE pandocversionout)

string(REGEX MATCH "([0-9]+\\.?)+" PANDOC_VER "${pandocversionout}")

if (PANDOC_VER VERSION_LESS "1.13.2")
  message(FATAL_ERROR "pandoc version 1.13.2 or greater required")
endif()


Function(AddPDF docName)
  set(output_name ${CMAKE_BINARY_DIR}/${docName}/${docName}.pdf)
  add_custom_command(OUTPUT ${output_name}

    COMMAND ${WKHTMLTOPDF_EXECUTABLE} -q --enable-smart-shrinking --page-size Letter --load-error-handling abort --footer-left "Copyright 2015 Jason Turner"  --footer-center "@lefticus" --footer-right "[page] / [topage]" --javascript-delay 500 -O Landscape -s Letter file:///${CMAKE_BINARY_DIR}/${docName}/${docName}.html?print-pdf ${output_name}
    DEPENDS ${CMAKE_BINARY_DIR}/${docName}/${docName}.html
    WORKING_DIRECTORY ${CMAKE_BINARY_DIR}/${docName}
    )

  add_custom_target(${docName}_pdf ALL
    DEPENDS ${output_name}
    )


EndFunction()


Function(BuildDocument docName)
  set(output_name ${CMAKE_BINARY_DIR}/${docName}/${docName}.html)

  foreach(input_name ${ARGN})
    list(APPEND input_names ${CMAKE_CURRENT_SOURCE_DIR}/${input_name})
  endforeach()

  list(GET input_names 0 MD_FILE)

  add_custom_command(OUTPUT ${output_name}
    COMMAND ${CMAKE_COMMAND} -E remove_directory ${CMAKE_BINARY_DIR}/${docName}/reveal.js
    COMMAND ${CMAKE_COMMAND} -E make_directory   ${CMAKE_BINARY_DIR}/${docName}/reveal.js
    COMMAND ${CMAKE_COMMAND} -E make_directory   ${CMAKE_BINARY_DIR}/${docName}/reveal.js/js
    COMMAND ${CMAKE_COMMAND} -E copy_directory   ${CMAKE_CURRENT_SOURCE_DIR}/reveal.js/js ${CMAKE_BINARY_DIR}/${docName}/reveal.js/js
    COMMAND ${CMAKE_COMMAND} -E make_directory   ${CMAKE_BINARY_DIR}/${docName}/reveal.js/css
    COMMAND ${CMAKE_COMMAND} -E copy_directory   ${CMAKE_CURRENT_SOURCE_DIR}/reveal.js/css ${CMAKE_BINARY_DIR}/${docName}/reveal.js/css
    COMMAND ${CMAKE_COMMAND} -E make_directory   ${CMAKE_BINARY_DIR}/${docName}/reveal.js/lib
    COMMAND ${CMAKE_COMMAND} -E copy_directory   ${CMAKE_CURRENT_SOURCE_DIR}/reveal.js/lib ${CMAKE_BINARY_DIR}/${docName}/reveal.js/lib
    COMMAND ${CMAKE_COMMAND} -E make_directory   ${CMAKE_BINARY_DIR}/${docName}/reveal.js/plugin
    COMMAND ${CMAKE_COMMAND} -E copy_directory   ${CMAKE_CURRENT_SOURCE_DIR}/reveal.js/plugin ${CMAKE_BINARY_DIR}/${docName}/reveal.js/plugin

    COMMAND ${CMAKE_COMMAND} -E remove_directory ${CMAKE_BINARY_DIR}/${docName}/highlight.js
    COMMAND ${CMAKE_COMMAND} -E make_directory   ${CMAKE_BINARY_DIR}/${docName}/highlight.js
    COMMAND ${CMAKE_COMMAND} -E make_directory   ${CMAKE_BINARY_DIR}/${docName}/highlight.js/styles
    COMMAND ${CMAKE_COMMAND} -E copy ${CMAKE_CURRENT_SOURCE_DIR}/highlight.js/highlight.pack.js ${CMAKE_BINARY_DIR}/${docName}/highlight.js/
    COMMAND ${CMAKE_COMMAND} -E copy_directory   ${CMAKE_CURRENT_SOURCE_DIR}/highlight.js/styles ${CMAKE_BINARY_DIR}/${docName}/highlight.js/styles

    COMMAND ${CMAKE_COMMAND} -E copy ${input_names} ${CMAKE_BINARY_DIR}/${docName}/


    COMMAND ${PANDOC_EXECUTABLE} ${MD_FILE} -o ${output_name} --no-highlight --css reveal.js/css/theme/lefticus.css -f markdown_github -s -t revealjs -A ${CMAKE_CURRENT_SOURCE_DIR}/revealjs-header.html  -A ${CMAKE_CURRENT_SOURCE_DIR}/highlightjs-header.html  --email-obfuscation=none
    #COMMAND ${PANDOC_EXECUTABLE} ${input_names} -o ${output_name} --no-highlight --css reveal.js/css/theme/lefticus.css -f markdown_github -s -t revealjs -A ${CMAKE_CURRENT_SOURCE_DIR}/revealjs-header.html   --email-obfuscation=none
    DEPENDS ${input_names} ${CMAKE_CURRENT_SOURCE_DIR}/highlightjs-header.html  ${CMAKE_CURRENT_SOURCE_DIR}/revealjs-header.html ${CMAKE_CURRENT_SOURCE_DIR}/reveal.js/css/theme/lefticus.css
    #DEPENDS ${input_names}   ${CMAKE_CURRENT_SOURCE_DIR}/revealjs-header.html ${CMAKE_CURRENT_SOURCE_DIR}/reveal.js/css/theme/lefticus.css
    )

  #  add_custom_target(${docName}_html ALL
  #  DEPENDS ${output_name}
  #  )


  AddPDF(${docName})
EndFunction()

BuildDocument(ThinkingPortable ThinkingPortable.md)
BuildDocument(CurrentStateOfStaticAnalysis CurrentStateOfStaticAnalysis.md)
BuildDocument(CrazyEasyScriptingWithChaiScript CrazyEasyScriptingWithChaiScript.md)
BuildDocument(PracticalPerformancePractices PracticalPerformancePractices.md chai-timing.svg MSVC_ChaiProfiling.png optimized_parse_node_graph.dot optimized_parse_node_graph.svg parse_node_graph.dot parse_node_graph_labeled.dot parse_node_graph_labeled.svg parse_node_graph.svg performance_monitoring.png)
BuildDocument(PracticalPerformancePractices-CppCon2016 PracticalPerformancePractices-CppCon2016.md chai-timing.svg MSVC_ChaiProfiling.png optimized_parse_node_graph.dot optimized_parse_node_graph.svg parse_node_graph.dot parse_node_graph_labeled.dot parse_node_graph_labeled.svg parse_node_graph.svg performance_monitoring.png)
BuildDocument(HowAndWhyToAddScripting HowAndWhyToAddScripting.md)



add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/CrazyEasyScriptingWithChaiScript)
